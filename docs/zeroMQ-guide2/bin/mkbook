#! /usr/bin/perl
#
#   mkbook - convert ZeroMQ document to DocBook format
#   - all images must already have been generated
#
#   Usage: bin/mkbook chapter*.txt; docbook2ps book.xml; ps2pdf book.ps
#
#   Creates Wikidot file as specified by .output and images in images/.
#
#   By Pieter Hintjens, free for remixing without conditions.
#
require 'sflcvdp.pl';                   #   SFL date picture formatting
use Digest::SHA1 qw(sha1_hex);

#   Language for the examples
$source = $ENV {'BOOK_LANG'} || "C";
#   Output format, book or pdf
$format = $ENV {'BOOK_FORM'} || "book";

printf "Generating $source/$format version of Guide...\n";

#   Listings longer than this are truncated and turned into URLs
$cutoff = 60;

#   Languages we support
%lexer = ('C' => 'c' ,
          'C++' => 'cpp',
          'Haxe' => 'haxe',
          'Lua' => 'lua',
          'PHP' => 'php',
          'Python' => 'python' );
          
#   File extensions - copied from z2w
%extension = ('Ada' => 'ada',
              'Basic' => 'bas',
              'C' => 'c',
              'C++' => 'cpp',
              'C#' => 'cs',
              'CL' => 'lisp',
              'Clojure' => 'clj',
              'Erlang' => 'es',
              'F#' => 'fsx',
              'Felix' => 'flx',
              'Go' => 'go',
              'Haskell' => 'hs',
              'Haxe' => 'hx',
              'Java' => 'java',
              'Lua' => 'lua',
              'Node.js' => 'js',
              'Objective-C' => 'm',
              'ooc' => 'ooc',
              'Perl' => 'pl',
              'PHP' => 'php',
              'Python' => 'py',
              'Q' => 'q',
              'Racket' => 'rkt',
              'Ruby' => 'rb',
              'Scala' => 'scala',
              'Tcl' => 'tcl' );

signatures_load ();

#   When filtering, we drop text that we don't want in the printed book
$filter = 0;

die "Can't create book.xml: $!"
    unless open (OUTPUT, ">book.xml");

print OUTPUT "<?xml version=\"1.0\"?>\n";
print OUTPUT "<!DOCTYPE book PUBLIC \"-//OASIS//DTD DocBook XML V4.5//EN\"\n";
print OUTPUT "\"http://www.oasis-open.org/docbook/xml/4.5/docbookx.dtd\">\n";
print OUTPUT "<book>\n";

#   Include bookinfo if publisher directory exists
if ($format eq "book" && open (BOOKINFO, "book/bookinfo.xml")) {
    print OUTPUT "<title>ZeroMQ - Connecting your Code</title>\n";
    while (<BOOKINFO>) {
        print OUTPUT $_ unless /DOCTYPE/;
    }
}
else {
    print OUTPUT "<title>The ZeroMQ Guide - for $source Developers</title>\n";
    print OUTPUT "<bookinfo>\n";
    print OUTPUT "  <isbn><!-- ISBN goes here --></isbn>\n";
    print OUTPUT "</bookinfo>\n";
}

start_block (1, "dedication");

while (<>) {
    chop while /\s$/;
    if (/^\./) {
        #   Process directive
        if (/^\.set\s+(\w+)=(.*)/) {
            $symbols {$1} = $2;
        }
        elsif (/^\.\-/) {
            #   Comment, ignore
        }
        elsif (/^\.end/) {
            last;
        }
        elsif (/^\.output\s+(\w.*)/) {
            #   Ignore .output directives here
        }
        elsif (/^\.prelude\s+(\w.*)/) {
            $prelude = $1;
        }
        elsif (/^\.filter\s+(\w.*)/) {
            $filter = ($1 eq $format);
        }
        elsif (/^\.filter reset/) {
            $filter = 0;
        }
        else {
            print "Illegal directive $_ at line $.\n";
        }
    }
    elsif (/^\+\+\s+Chapter\s+\w+\s+-\s+(.*)$/) {
        close_list ();
        start_block (1, "chapter");
        writeln (requote ("<title>$1</title>"));
    }
    elsif (/^\+\+\+\s+(.*)$/) {
        close_list ();
        start_block (2, "sect1");
        writeln (requote ("<title>$1</title>"));
    }
    elsif (/^\+\+\+\+\s+(.*)$/) {
        close_list ();
        start_block (3, "sect2");
        writeln (requote ("<title>$1</title>"));
    }
    #  Process unnamed code listing
    elsif (/\[\[code\s+language=\"(.+)\"\]\]/) {
        close_list ();
        $language = $1;
        $lexer = $lexer {$language};
        writeln ("<programlisting language=\"$lexer\">");
        while (<>) {
            chop;
            last if /\[\[\/code\]\]/;
            writesrc ($_);
        }
        writeln ("</programlisting>\n");
    }
    #  Process named codeblock
    elsif (/\[\[code\s+type=\"example\"\s+title=\"([^"]+)\"\s+name=\"([^"]+)\"(\s+language=\"([^"]+)\")?\]\]/) {
        close_list ();
        $title = $1;
        $name = $2;
        $language = $4 || $source;

        $lexer = $lexer {$language};
        $ext = $extension {$language};
        $filename = "examples/$language/$name.$ext";

        writeln ("<example id=\"$name-$ext\">");
        writeln ("<title>$title ($name.$ext)</title>");
        writeln ("<programlisting language=\"$lexer\">");
        
        if (open (EXAMPLE, $filename)) {
            $lines = 0;
            $split = 0;
            $blanks = 0;
            $start = 0;
            while (<EXAMPLE>) {
                chop;
                if (/\/\/  \.split\s(.*)/) {
                    #   Long code example, split into separate pieces
                    writeln ("</programlisting>\n");
                    writeln ("</example>");
                    printf ("$filename: $lines lines ($start)\n") if $lines > $cutoff;
                    $text = "";
                    $start = $.;
                    $comment = $1;
                    while (<EXAMPLE>) {
                        chop while /\s$/;
                        last unless /\/\/  /;   #   End at any non-comment line
                        $text .= "$' ";
                    }
                    $split++;
                    writeln (requote ("<para>$text\n</para>\n"));
                    writeln ("<example id=\"$name-$ext-$split\">");
                    writeln ("<title>$title ($name.$ext) - $1</title>");
                    writeln ("<programlisting language=\"$lexer\">");
                    $lines = 0;
                }
                elsif (/\/\/  \.skip/) {
                    writesrc ("...");
                    while (<EXAMPLE>) {
                        chop while /\s$/;
                        last if /\/\/  \.until/;
                    }
                }
                else {
                    #   Don't output trailing blank lines
                    if (/^$/) {
                        $blanks++;
                    }
                    else {
                        print OUTPUT "\n" x $blanks;
                        $blanks = 0;
                        writesrc ($_);
                    }
                    $lines++;
                }
            }
            close (EXAMPLE);
            printf ("$filename: $lines lines ($start)\n")
                if $lines > $cutoff
                && $format eq "book";
        }
        else {
            writesrc ("(This example still needs translation into $source)");
        }
        writeln ("</programlisting>\n");
        writeln ("</example>");

        #   Discard any real contents of code block
        while (<>) {
            last if /\[\[\/code\]\]/;
            print "W: discarding code for $title - $name\n";
        }
    }
    #  Process plain code block
    elsif (/\[\[code\]\]/) {
        close_list ();
        writect ("<screen>");
        while (<>) {
            chop;
            last if /\[\[\/code\]\]/;
            writesrc ($_);
        }
        writeln ("</screen>\n");
    }
    elsif (/\[\[code type=\"textdiagram\"\s+title=\"([^"]+)\"\]\]/) {
        close_list ();
        #   Create text file for diagram
        $title = $1;
        $figure = $figure + 1;
        $filename = "images/fig$figure.txt";
        die "Can't create $filename: $!"
            unless open (IMAGE, ">$filename");
        $longest = 0;
        while (<>) {
            chop;
            last if /\[\[\/code\]\]/;
            #   DitaaEps uses Ditaa 0.6b, which doesn't support {o}
            s/\{o\}/   /g;
            $longest = length ($_) if length ($_) > $longest;
            print IMAGE "$_\n";
        }
        close (IMAGE);
        
        #   Calculate scaling so image looks decent
        $scale = 40 / $longest;
        $scale = 1 if $scale > 1;

        #   Convert diagram into image of required format
        if (signature_changed ($filename)) {
            print "Converting $filename to EPS image...\n";
            system ("java -jar bin/DitaaEps/DitaaEps.jar -s $scale $filename images/fig$figure.eps");
            signature_update ($filename);
        }
        writeln ("<figure id=\"figure-$figure\">");
        writeln ("    <title>$title</title>");
        writeln ("    <mediaobject>");
        writeln ("        <imageobject>");
        writeln ("            <imagedata fileref=\"images/fig$figure.eps\" format=\"EPS\"/>");
        writeln ("        </imageobject>");
        writeln ("    </mediaobject>");
        writeln ("</figure>\n");
    }
    elsif (/\[\[code/) {
        close_list ();
        print "E: unparsed code block: $_\n";
    }
    elsif (/^\* /) {
        require_list ("itemizedlist");
        writeln (requote ("  <listitem><para>$'</para></listitem>"));
    }
    elsif (/^# /) {
        require_list ("orderedlist");
        writeln (requote ("  <listitem><para>$'</para></listitem>"));
    }
    elsif ($_) {
        #   Normal text
        close_list ();
        writeln (requote ("<para>$_</para>\n")) unless $filter;
    }
}
close_list ();
close_blocks ();
writeln ("</book>");
close (OUTPUT);

signatures_save ();
exit (0);

#   Open and close block elements

sub start_block {
    local ($level, $name) = @_;
    #   Close any open blocks at this or higher levels
    while ($cur_level >= $level) {
        writeln ("</".$block [$cur_level].">");
        $cur_level--;
    }
    writeln ("<$name>");
    $block [$level] = $name;
    $cur_level = $level;
}

sub close_blocks {
    while ($cur_level > 0) {
        writeln ("</".$block [$cur_level].">");
        $cur_level--;
    }
}

#   Writes $_ to OUTPUT after expanding all symbols

sub writeln {
    local ($_) = @_;
    $_ = expand_symbols ($_);
    print OUTPUT "$_\n";
}

sub writect {
    local ($_) = @_;
    $_ = expand_symbols ($_);
    print OUTPUT "$_";
}

sub writesrc {
    local ($_) = @_;
    s/&/&amp;/g;
    s/</&lt;/g;
    s/>/&gt;/g;
    print OUTPUT "$_\n";
}

#   Do smart curly quotes before we add any tags

sub requote {
    local ($_) = @_;
#    s/"([a-zA-ZØ0-9.\/\(])/&#8220;\1/g;
#    s/([a-zA-Z0-9!?.\/\)])"/\1&#8221;/g;
#    s/ '([a-zA-Z0-9])/ &#8216;\1/g;
#    s/'/&#8217;/g;
    #print "$_\n" if /"/;
    return $_;
}

#   Manage lists

sub require_list {
    local ($list) = @_;
    close_list if $list ne $cur_list;
    $cur_list = $list;
    if (!$in_list) {
        writeln ("<$cur_list>");
        $in_list = 1;
    }
}

sub close_list {
    if ($in_list) {
        $in_list = 0;
        writeln ("</$cur_list>");
    }
}


#   Load signatures hash tables
sub signatures_load {
    print "I: loading signatures... ";
    undef %signatures;
    local $count = 0;
    if (open (SIGNATURES, ".signatures")) {
        while (<SIGNATURES>) {
            /([^\s]+)\s+([^\s]+)/;
            $signatures {$2} = $1;
            $count++;
        }
    }
    print "$count loaded\n";
}

#   If file signature has changed, return 1, else 0
sub signature_changed {
    local ($filename) = @_;
    $_ = `sha1sum $filename`;
    if (/([^\s]+)\s+/) {
        if ($signatures {$filename} eq $1) {
            return 0;
        }
        else {
            return 1;
        }
    }
    else {
        "E: can't sha1sum $filename\n";
        return 0;
    }
}

#   Save file signature into table
sub signature_update {
    local ($filename) = @_;
    $_ = `sha1sum $filename`;
    if (/([^\s]+)\s+/) {
        $signatures {$filename} = $1;
    }
    else {
        "E: can't sha1sum $filename\n";
        return 0;
    }
}

#   Save signatures table
sub signatures_save {
    print "I: saving signatures... ";
    local $count = 0;
    local $filename;
    if (open (SIGNATURES, ">.signatures")) {
        foreach $filename (keys %signatures) {
            print SIGNATURES "$signatures{$filename} $filename\n";
            $count++;
        }
    }
    print "$count saved\n";
}


#-- Symbol expansion code, taken from htmlpp

#   Recursively expand symbols like this (and in this order):
#
#   $(xxx)                  - value of variable
#   $(xxx?zzz)              - value of variable, or zzz if undefined
#   %(text?zzz)             - value of environment variable, or zzz if undef
#   !abc(text)              - intrinsic function with arguments
#
sub expand_symbols {
    local ($_) = @_;
    local ($before,
           $match,
           $after,
           $expr);

    return unless ($_);                 #   Quit if input string is empty

    $_ = zmq_symbol ($_);

    for (;;) {
        #   Force expansion from end of string first, so things like
        #   $(xxx?$(yyy)) work properly.
        if (/[\$%]\(/ || /\!([a-z_]+)\s*\(/i) {
            $before = $`;
            $match  = $&;
            $after  = expand_symbols ($');
            $_ = $before.$match.$after;
        }
        #   $(xxx)
        if (/\$\(([A-Za-z0-9-_\.]+)\)/) {
            $_ = $`.&valueof ($1).$';
        }
        #   $(xxx?zzz)
        elsif (/\$\(([A-Za-z0-9-_\.]+)\?([^)\$]*)\)/) {
            $_ = $`.&valueof ($1, $2).$';
        }
        #   %(text)
        elsif (/\%\(([^\)]+)\)/) {
            $_ = $`.$ENV {$1}.$';
        }
        #   %(text?zzz)
        elsif (/\%\(([^\)]+)\?([^)\$]*)\)/) {
            $_ = $`.($ENV {$1}? $ENV {$1}: $2).$';
        }
        #   !abc(text)
        elsif (/\!([a-z_]+)\s*\(([^\)]*)\)/i) {
            $funct = $1;
            $args = $2;
            $before = $`;
            $after = $';
            $args =~ s/\\/\\\\/g;
            $args =~ s/#/"/g;
            $_ = eval ("&intrinsic_$funct ($args)");
            $_ = $before.$_.$after;
            if ($@) {                   #   Syntax error in Perl statement?
                &error ("$funct is not a valid intrinsic function")
                    unless $nofunc_mode;
                last;
            }
        }
        else {
            last;
        }
    }
    #   Translate email addresses
    s/\<([\w\.]+@[\w\.]+)\>/<email>$1<\/email>/g;

    #   Translate character styles
    s/([^:])\/\/([^\/]+)\/\//$1<emphasis>$2<\/emphasis>/g;
    s/\*\*([^*]+)\*\*/<emphasis role="bold">$1<\/emphasis>/g;
    s/\{\{([^{]+)\}\}/<literal>$1<\/literal>/g;

    #   Translate normal hyperlinks
    s/\[(http:\/\/[^ ]+) ([^\]]+)\]/<ulink url="$1">$2<\/ulink>/g;
    s/\[(https:\/\/[^ ]+) ([^\]]+)\]/<ulink url="$1">$2<\/ulink>/g;

    #   Translate footnotes
    s/\[\[footnote\]\]([^\]]+)\[\[\/footnote\]\]/<footnote>$1<\/footnote>/g;

    #   Translate links into zguide.zeromq.org
    s/\[\/([^ ]+) ([^\]]+)\]/<ulink url="http:\/\/zguide.zeromq.org\/$1">$2<\/ulink>/g;

    #   Translate 0MQ into ØMQ unless escaped
    s/0MQ/ØMQ/g;
    s/0\\MQ/0MQ/g;

    #   Translate accented characters, presumably there's a module
    #   that will do this properly...
    s/Å/&Aring;/g;
    s/É/&Eacute;/g;
    s/è/&egrave;/g;
    s/ø/&oslash;/g;
    s/ő/&ouml;/g;
    s/Ø/&Oslash;/g;

    return $_;
}


#   Translate 0MQ into ØMQ and 0\MQ into 0MQ
sub zmq_symbol {
    local ($_) = @_;

    s/0MQ/ØMQ/g;
    s/0\\MQ/0MQ/g;
    return $_;
}


#   Subroutine returns the value of the specified symbol; it issues a
#   warning message and returns 'UNDEF' if the symbol is not defined
#   and the default value is empty.
#
sub valueof {
    local ($symbol, $default) = @_;     #   Argument is symbol name
    local ($return);                    #   Returned value
    local ($langed_symbol);             #   Language-dependent symbol

    if (defined ($symbols {$symbol})) {
        $return = $symbols {$symbol};
        return $return;
    }
    elsif (defined ($default)) {
        return ($default);
    }
    &error ("$_");
    &error ("($.) undefined symbol \"$symbol\"");
    $default_warning == 1 || do {
        &error ("I: Use \$($symbol?default) for default values.");
        $default_warning = 1;
    };
    $symbols {$symbol} = "UNDEF";
    return $symbols {$symbol};
}


#   INTRINSIC FUNCTIONS
#
#   figref()                   - Insert '(<xref linkend="figure-nnn"/>)'
#   time()                     - Format current time as hh:mm:ss
#   date()                     - Return current date value
#   date(#picture#)            - Format current date using picture
#   date(#picture#, date, lc)  - Format specified date using picture & language
#   week_day([date])           - Get day of week, 0=Sunday to 6=Saturday
#   year_week([date])          - Get week of year, 1 is first full week
#   julian_date([date])        - Get Julian date for date
#   lillian_date([date])       - Get Lillian date for date
#   date_to_days(date)         - Convert yyyymmdd to Lillian date
#   days_to_date(days)         - Convert Lillian date to yyyymmdd
#   future_date(days[,date])   - Calculate a future date
#   past_date(days[,date])     - Calculate a past date
#   date_diff(date1[,date2])   - Calculate date1 - date2
#   image_height(#image.ext#)  - Get image height (GIF, JPEG)
#   image_width(#image.ext#)   - Get image width (GIF, JPEG)
#   file_size(#filename#,arg)  - Get size of file: optional arg K or M
#   file_date(#filename#)      - Get date of file
#   file_time(#filename#)      - Get time of file as hh:mm:ss
#   normalise(#filename#)      - Normalise filename to UNIX format
#   system(#command#)          - Call a system utility
#   lower(#string#)            - Convert string to lower case
#   upper(#string#)            - Convert string to upper case
#

sub intrinsic_figref {
    local $nextfig = $figure + 1;
    return "(<xref linkend=\"figure-$nextfig\"/>)";
}

sub intrinsic_date {
    local ($picture, $value, $language) = @_;
    $value = &date_now unless $value;
    $language = $symbols{LANG} unless $language;
    if ($picture) {
        return (&conv_date_pict ($value, $picture, $language));
    }
    else {
        return ($value);
    }
}

sub intrinsic_time {
    local ($sec, $min, $hour, $day, $month, $year) = localtime;
    return (sprintf ("%2d:%02d:%02d", $hour, $min, $sec));
}

sub intrinsic_week_day {
    return (&day_of_week ($_ [0]? $_ [0]: &date_now));
}

sub intrinsic_year_week {
    return (&week_of_year ($_ [0]? $_ [0]: &date_now));
}

sub intrinsic_julian_date {
    return (&julian_date ($_ [0]? $_ [0]: &date_now));
}

sub intrinsic_lillian_date {
    return (&date_to_days ($_ [0]? $_ [0]: &date_now));
}

sub intrinsic_date_to_days {
    return (&date_to_days ($_ [0]));
}

sub intrinsic_days_to_date {
    return (&days_to_date ($_ [0]));
}

sub intrinsic_future_date {
    local ($date) = &future_date ($_ [1], 0, $_ [0], 0);
    return ($date);
}

sub intrinsic_past_date {
    local ($date) = &past_date ($_ [1], 0, $_ [0], 0);
    return ($date);
}

sub intrinsic_date_diff {
    local ($date1, $date2) = @_;
    $date1 = &date_now unless $date1;
    $date2 = &date_now unless $date2;
    local ($days) = &date_diff ($date1, 0, $date2, 0);
    return ($days);
}

sub intrinsic_image_height {
    local ($filename) = @_;
    if (! -e $filename) {
        &error ("($.) file not found: \"$filename\"");
    }
    else {
        return (&image_height ($filename));
    }
}

sub intrinsic_image_width {
    local ($filename) = @_;
    if (! -e $filename) {
        &error ("($.) file not found: \"$filename\"");
    }
    else {
        return (&image_width ($filename));
    }
}

sub intrinsic_file_size {
    local ($filename, $arg) = @_;
    local ($size) = (stat ($filename)) [7];

    if (! -e $filename) {
        &error ("($.) file not found: \"$filename\"");
    }
    elsif ($arg eq "K") {
        $size /= 1024;
    }
    elsif ($arg eq "M") {
        $size /= 1048576;
    }
    return (int ($size));
}

sub intrinsic_file_date {
    local ($filename) = @_;
    if (! -e $filename) {
        &error ("($.) file not found: \"$filename\"");
    }
    else {
        local ($mtime) = (stat ($filename)) [9];
        local ($sec,$min,$hour,$mday,$mon,$year) = localtime ($mtime);
        return (($year + 1900) * 10000 + ($mon + 1) * 100 + $mday);
    }
}

sub intrinsic_file_time {
    local ($filename) = @_;
    if (! -e $filename) {
        &error ("($.) file not found: \"$filename\"");
    }
    else {
        local ($mtime) = (stat ($filename)) [9];
        local ($sec,$min,$hour,$mday,$mon,$year) = localtime ($mtime);
        return (sprintf ("%2d:%02d:%02d", $hour, $min, $sec));
    }
}

sub intrinsic_normalise {
    local ($_) = @_;                    #   Get filename argument
    s/\\/\//g;                          #   Replace DOS-style \ by /
    s/\s/_/g;                           #   Replace white space by _
    return ($_);
}

sub intrinsic_system {
    local ($_) = `@_`;

    #   Return all but the last character, which should be a newline
    chop;
    return ($_);
}

sub intrinsic_lower {
    local ($_) = @_;                    #   Get filename argument
    tr/A-Z/a-z/;
    return ($_);
}

sub intrinsic_upper {
    local ($_) = @_;                    #   Get filename argument
    tr/a-z/A-Z/;
    return ($_);
}

sub error {
    ($_) = @_;                          #   Get argument
    print STDERR "E: $_\n";
    $have_errors = 1;                   #   We have 1 or more errors
}

